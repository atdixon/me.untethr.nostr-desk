(ns me.untethr.nostr.timeline-support
  (:require
   [clojure.tools.logging :as log]
   [me.untethr.nostr.domain :as domain]
   [loom.graph :as loom]
   [loom.attr :as loom-attr])
  (:import
   (me.untethr.nostr.domain UITextNote UITextNoteWrapper)))

(defn- create-node->num-predecessors
  [graph]
  (reduce (fn [acc n]
            (assoc acc n (count (loom/predecessors graph n))))
    {} (loom/nodes graph)))

(defn- likely-root
  [graph]
  (let [num-predecessors (create-node->num-predecessors graph)]
    (first (apply max-key val num-predecessors))))

(defn- contribute!*
  [graph id parent-ids]
  (as-> graph G
    (apply loom/add-nodes G (cons id parent-ids))
    (apply loom/add-edges G (map #(vector id %1) parent-ids))
    ;; expect parent-ids to be in order and we'll create edges for [:a :b :c]
    ;;   like so :c -> :b -> :a
    (apply loom/add-edges G (map vec (partition 2 1 (reverse parent-ids))))))

(defn- ->note
  ^UITextNote [pruned-graph n seen?]
  (let [seen? (conj seen? n)
        kids (mapv #(->note pruned-graph % seen?)
               (filter (complement seen?) (loom/predecessors pruned-graph n)))]
    (if-let [{:keys [id pubkey created_at content etag-ids]} (loom-attr/attr pruned-graph n ::data)]
      (domain/->UITextNote id pubkey content created_at etag-ids kids)
      (domain/->UITextNote n nil (format "<missing:%s>" n) nil [] kids))))

(defn- build*
  [graph]
  ;; shed all edges to root for nodes deeper than one
  (let [use-root (likely-root graph)
        graph' (apply loom/remove-edges graph
                 (keep
                   (fn [n]
                     (let [out (loom/out-edges graph n)]
                       (when (> (count out) 1)
                         [n use-root])))
                   (loom/nodes graph)))]
    (->note graph' use-root #{})))

(defn contribute!
  ^UITextNoteWrapper
  [^UITextNoteWrapper wrapper {:keys [id created_at] :as event-obj} etag-ids]
  ;; note: we expect one of id or etag-ids to exist in the provided wrapper's
  ;;    :loom-graph (or the :loom-graph should be empty) and we expect the
  ;;    :loom-graph to be connected; this implies that our contributions here
  ;;    will also leave the graph fully connected.
  (let [graph (contribute!* (:loom-graph wrapper) id etag-ids)
        graph (loom-attr/add-attr graph id
                ::data (-> event-obj
                         (select-keys [:id :pubkey :created_at :content])
                         (assoc :etag-ids etag-ids)))]
    (assoc wrapper
      :loom-graph graph
      :note-count (count (loom/nodes graph))
      :max-timestamp (max (:max-timestamp wrapper) created_at)
      :root (build* graph))))

(defn init!
  ^UITextNoteWrapper [event-obj etag-ids]
  (let [x (domain/->UITextNoteWrapper (loom/digraph) true 0 -1 nil)]
    (contribute! x event-obj etag-ids)))
